(# Get the params)
(var uuid (get_param "uuid"))

(# Get the access token)
(var access_token (get_header "Authorization"))

(# Get the session)
(if (!(is_nil access_token)) (var session (func get_session (access_token))))

(# Check if the user is an admin)
(var is_admin (
	(if (is_nil session) (
		(return false)
	) else (
		(var admins (get_env "ADMINS"))
		(admins.contains session.user_id)
	))
))

(# Get the store book)
(var store_book (func get_table_object (uuid (get_env "STORE_BOOK_TABLE_ID"))))

(if (is_nil store_book) (
	(# StoreBook does not exist)
	(func render_validation_errors ((list (hash (error (get_error 3508)) (status 404)))))
))

(# Get the status)
(var status (if (is_nil store_book.properties.status) "unpublished" else store_book.properties.status))

(# Check if the user can access the store book)
(var can_access (
	(if (status == "published") (
		(return true)
	) elseif is_admin (
		(return true)
	) elseif (is_nil session) (
		(return false)
	) else (
		(# Check if the user is the author of the store book)
		(return (session.user_id == store_book.user_id))
	))
))

(if (! can_access) (
	(# Action not allowed)
	(func render_validation_errors ((list (hash (error (get_error 1002)) (status 403)))))
))

(# Get the categories)
(var categories (func get_category_keys_by_string (store_book.properties.categories)))

(# Return the store book)
(render_json 
	(hash 
		(uuid store_book.uuid)
		(collection store_book.properties.collection)
		(title store_book.properties.title)
		(description (if (is_nil store_book.properties.description) "" else store_book.properties.description))
		(language store_book.properties.language)
		(price (to_int store_book.properties.price))
		(isbn store_book.properties.isbn)
		(status status)
		(cover (!(is_nil store_book.properties.cover)))
		(cover_aspect_ratio store_book.properties.cover_aspect_ratio)
		(cover_blurhash store_book.properties.cover_blurhash)
		(file (!(is_nil store_book.properties.file)))
		(file_name store_book.properties.file_name)
		(categories categories)
		(in_library (func is_store_book_in_user_library (session.user_id store_book.uuid)))
		(purchased (func has_user_purchased_table_object (session.user_id store_book.id)))
	) 
	200
)