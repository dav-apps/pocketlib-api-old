(# Get the params)
(var uuid (get_param "uuid"))

(var json (parse_json (get_body)))

(var title json.title)
(var description json.description)
(var language json.language)
(var price json.price)
(var isbn json.isbn)
(var status json.status)
(var categories json.categories)

(# Get the access token)
(var access_token (get_header "Authorization"))
(func render_validation_errors ((list
	(func validate_auth_header_presence (access_token))
)))

(# Make sure content type is json)
(func render_validation_errors ((list
	(func validate_content_type_json ((get_header "Content-Type")))
)))

(# Get the session)
(var session (func get_session (access_token)))

(# Check if the user is an admin)
(var admins (get_env "ADMINS"))
(var is_admin (admins.contains session.user_id))

(# Get the store book)
(var store_book (func get_table_object (uuid (get_env "STORE_BOOK_TABLE_ID"))))

(if (is_nil store_book) (
	(# Render Resource does not exist: StoreBook)
	(func render_validation_errors ((list (hash (error (get_error 3508)) (status 404)))))
))

(# Get the latest store book release)
(var store_book_release (func get_last_release_of_store_book (store_book false)))

(if (is_nil store_book_release) (
	(# StoreBookRelease does not exist)
	(func render_validation_errors ((list (hash (error (get_error 3516)) (status 404)))))
))

(# Get the collection of the store book)
(var collection (func get_table_object (store_book.properties.collection (get_env "STORE_BOOK_COLLECTION_TABLE_ID"))))

(if (is_nil collection) (
	(# Collection does not exist)
	(func render_validation_errors ((list (hash (error (get_error 3506)) (status 404)))))
))

(# Get the author of the collection)
(var author (func get_table_object (collection.properties.author (get_env "AUTHOR_TABLE_ID"))))

(if (is_nil author) (
	(# Author does not exist)
	(func render_validation_errors ((list (hash (error (get_error 3503)) (status 404)))))
))

(# Check if the user is an admin or if the store book belongs to the author of the user)
(if ((! is_admin) and (session.user_id != author.user_id)) (
	(# Action not allowed)
	(func render_validation_errors ((list (hash (error (get_error 1002)) (status 403)))))
))

(# Validate the property types)
(func render_validation_errors ((list
	(if (!(is_nil title)) (func validate_title_type (title)))
	(if (!(is_nil description)) (func validate_description_type (description)))
	(if (!(is_nil language)) (func validate_language_type (language)))
	(if (!(is_nil price)) (func validate_price_type (price)))
	(if (!(is_nil isbn)) (func validate_isbn_type (isbn)))
	(if (!(is_nil status)) (func validate_status_type (status)))
	(if (!(is_nil categories)) (func validate_categories_type (categories)))
)))

(# Validate the property length)
(func render_validation_errors ((list
	(if (!(is_nil title)) (func validate_title_length (title)))
	(if (!(is_nil description)) (func validate_description_length (description)))
)))

(if (!(is_nil language)) (
	(# Validate the language)
	(func render_validation_errors ((list
		(func validate_language_supported (language))
	)))
))

(# Validate price and isbn)
(func render_validation_errors ((list
	(if (!(is_nil price)) (func validate_price_validity (price)))
	(if (!(is_nil isbn)) (func validate_isbn_validity (isbn)))
)))

(if ((!(is_nil status)) and is_admin) (
	(# Validate the status)
	(func render_validation_errors ((list
		(func validate_status_supported (status))
	)))
))

(if (!(is_nil categories)) (
	(# Validate the length of the categories)
	(func render_validation_errors ((list
		(func validate_categories_length (categories))
	)))
))

(# Don't update language, price or isbn if the StoreBook is already published)
(if ((store_book.properties.status == "published") or (store_book.properties.status == "hidden")) (
	(if (!(is_nil language)) (
		(func render_validation_errors ((list (hash (error (get_error 3400)) (status 422)))))
	))

	(if (!(is_nil price)) (
		(func render_validation_errors ((list (hash (error (get_error 3401)) (status 422)))))
	))

	(if (!(is_nil isbn)) (
		(func render_validation_errors ((list (hash (error (get_error 3402)) (status 422)))))
	))
))

(# Check if the release is already published)
(if (store_book_release.properties.status == "published") (
	(# Create a new release)
	(var new_release (func create_table_object (
		store_book.user_id
		(get_env "STORE_BOOK_RELEASE_TABLE_ID")
		(hash
			(store_book store_book.uuid)
			(title store_book_release.properties.title)
			(description store_book_release.properties.description)
			(cover_item store_book_release.properties.cover_item)
			(file_item store_book_release.properties.file_item)
			(categories store_book_release.properties.categories)
		)
	)))

	(# Add the new release to the releases of the store book)
	(func add_release_to_store_book (store_book new_release.uuid))

	(var store_book_release new_release)
))

(# Update the values)
(if (!(is_nil title)) (var store_book_release.properties.title title))
(if (!(is_nil description)) (var store_book_release.properties.description description))
(if (!(is_nil language)) (var store_book.properties.language language))
(if (!(is_nil price)) (
	(var store_book.properties.price price)

	(# Set the price of the table object)
	(func set_table_object_price (store_book.uuid price "eur"))
))
(if (!(is_nil isbn)) (var store_book.properties.isbn (if (isbn.length == 0) nil else isbn)))

(def check_properties_for_publishing (store_book_release) (
	(var errors (list))

	(if (is_nil store_book_release.properties.description) (
		(errors.push (hash (error (get_error 3300)) (status 422)))
	))

	(if (is_nil store_book_release.properties.cover_item) (
		(errors.push (hash (error (get_error 3301)) (status 422)))
	))

	(if (is_nil store_book_release.properties.file_item) (
		(errors.push (hash (error (get_error 3302)) (status 422)))
	))

	(if (errors.length > 0) (
		(func render_validation_errors (errors))
	))
))

(if (!(is_nil status)) (
	(if is_admin (
		(if ((
			(is_nil store_book_release.properties.status)
			or (store_book_release.properties.status == "unpublished")
			or (store_book_release.properties.status == "review")
		) and (
			(status == "hidden")
			or (status == "published")
		)) (
			(# Check if the book can be published)
			(func check_properties_for_publishing (store_book_release))
		))

		(# Update the status)
		(var store_book.properties.status status)

		(if (status == "published") (
			(# Set the status of the release to "published")
			(var store_book_release.properties.status "published")

			(# Add the store book to the latest store books)
			(func add_store_book_to_latest_books_collection (store_book))
		))
	) else (
		(if (
			((is_nil store_book.properties.status) or (store_book.properties.status == "unpublished"))
			and (status == "review")
		) (
			(# Check if the book can be published)
			(func check_properties_for_publishing (store_book_release))

			(# Change the status of the book to "review")
			(var store_book.properties.status "review")
		) elseif ((store_book.properties.status == "review") and (status == "unpublished")) (
			(# Change the status of the book to "unpublished")
			(var store_book.properties.status "unpublished")
		) elseif ((store_book.properties.status == "hidden") and (status == "published")) (
			(# Change the status of the book to "published")
			(var store_book.properties.status "published")
		) elseif ((store_book.properties.status == "published") and (status == "hidden")) (
			(# Change the status of the book to "hidden")
			(var store_book.properties.status "hidden")
		) else (
			(# Action not allowed)
			(func render_validation_errors ((list (hash (error (get_error 1002)) (status 403)))))
		))
	))
))

(# Update the categories)
(if (!(is_nil categories)) (
	(var category_keys (list))

	(# Get the uuids of the categories)
	(var category_objects (func get_category_table_objects ()))
	(var category_uuids (list))

	(for key in categories (
		(# Check if the key is already in the category_keys list)
		(if (category_keys.contains key) (break))

		(# Find the category with the key)
		(for obj in category_objects (
			(if (obj.properties.key == key) (
				(category_uuids.push obj.uuid)
				(category_keys.push key)
				(break)
			))
		))
	))

	(var store_book_release.properties.categories (category_uuids.join ","))
) else (
	(# Get the categories)
	(var category_keys (func get_category_keys_by_string (store_book_release.properties.categories)))
))

(# Get the status)
(var status (if (is_nil store_book.properties.status) "unpublished" else store_book.properties.status))

(# Return the store book)
(render_json (hash 
	(uuid store_book.uuid)
	(title store_book_release.properties.title)
	(description (if (is_nil store_book_release.properties.description) "" else store_book_release.properties.description))
	(language store_book.properties.language)
	(price (to_int store_book.properties.price))
	(isbn store_book.properties.isbn)
	(status status)
	(categories category_keys)
) 200)