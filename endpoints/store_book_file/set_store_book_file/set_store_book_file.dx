(# Get the params)
(var uuid (get_param "uuid"))

(var data (get_body))

(# Get the access token)
(var access_token (get_header "Authorization"))
(func render_validation_errors ((list
	(func validate_auth_header_presence (access_token))
)))

(# Check if content type is supported)
(var content_type (get_header "Content-Type"))
(func render_validation_errors ((list
	(func validate_content_type_ebook (content_type))
)))

(# Get the session)
(var session (func get_session (access_token)))

(# Get the store book)
(var store_book (func get_table_object (uuid (get_env "STORE_BOOK_TABLE_ID"))))

(if (is_nil store_book) (
	(# Render Resource does not exist: StoreBook)
	(func render_validation_errors ((list (hash (error (get_error 3508)) (status 404)))))
))

(# Get the latest store book release)
(if (is_nil store_book.properties.releases) (
	(# StoreBookRelease does not exist)
	(func render_validation_errors ((list (hash (error (get_error 3516)) (status 404)))))
))

(var releases (store_book.properties.releases.split ","))
(var releases releases.reverse)
(var latest_release (func get_table_object ((releases.pop) (get_env "STORE_BOOK_RELEASE_TABLE_ID"))))

(if (is_nil latest_release) (
	(# StoreBookRelease does not exist)
	(func render_validation_errors ((list (hash (error (get_error 3516)) (status 404)))))
))

(# Get the collection of the store book)
(var collection (func get_table_object (store_book.properties.collection (get_env "STORE_BOOK_COLLECTION_TABLE_ID"))))

(if (is_nil collection) (
	(# Collection does not exist)
	(func render_validation_errors ((list (hash (error (get_error 3506)) (status 404)))))
))

(# Get the author of the collection)
(var author (func get_table_object (collection.properties.author (get_env "AUTHOR_TABLE_ID"))))

(if (is_nil author) (
	(# Resource does not exist: Author)
	(func render_validation_errors ((list (hash (error (get_error 3503)) (status 404)))))
))

(# Check if the store book belongs to the author of the user)
(if (session.user_id != author.user_id) (
	(# Action not allowed)
	(func render_validation_errors ((list (hash (error (get_error 1002)) (status 403)))))
))

(# Define functions for creating and updating the file table object)
(def set_filename (file_item) (
	(# Set the file name if there is one)
	(var content_disposition (get_header "Content-Disposition"))

	(if (! (is_nil content_disposition)) (
		(var matches (Regex.match content_disposition /filename=\"(?<filename>.*)\"/))
		(if (! (is_nil matches.filename)) (
			(var file_item.properties.file_name matches.filename)
		))
	))
))

(def create_file (store_book_release session content_type data) (
	(# Create the file table object)
	(var file (func create_store_book_file_table_object (store_book_release.user_id content_type data)))

	(# Create the file item table object)
	(var file_item (func create_table_object (
		store_book_release.user_id
		(get_env "STORE_BOOK_FILE_ITEM_TABLE_ID")
		(hash
			(file file.uuid)
		)
	)))

	(# Set the properties of the file item)
	(func set_filename (file_item))

	(return file_item)
))

(def update_file (file_item content_type data) (
	(# Update the file of the file table object)
	(func update_store_book_file_table_object (file_item.properties.file content_type data))

	(func set_filename (file_item))
))

(if (latest_release.status == "published") (
	(# Create a new release)
	(var new_release (func create_table_object (
		store_book.user_id
		(get_env "STORE_BOOK_RELEASE_TABLE_ID")
		(hash
			(store_book store_book.uuid)
			(title latest_release.properties.title)
			(description latest_release.properties.description)
			(cover_item latest_release.properties.cover_item)
			(categories latest_release.properties.categories)
		)
	)))

	(# Add the new release to the releases of the store book)
	(func add_release_to_store_book (store_book new_release.uuid))

	(# Create new file and file item)
	(var file_item (func create_file (new_release session content_type data)))
	(var new_release.properties.file_item file_item.uuid)
) else (
	(var release_uuid (releases.pop))
	(var release nil)
	(var update_file true)

	(if (!(is_nil release_uuid)) (
		(var release (func get_table_object (release_uuid (get_env "STORE_BOOK_RELEASE_TABLE_ID"))))

		(if (!(is_nil release)) (
			(# Check if the release is published and uses the file item of the latest release)
			(if (
				(release.properties.status == "published")
				and (release.properties.file_item == latest_release.properties.file_item)
			) (
				(var update_file false)
			))
		))
	))

	(if update_file (
		(# Update the file of the latest release)
		(var file_item (func get_table_object (latest_release.properties.file_item (get_env "STORE_BOOK_FILE_ITEM_TABLE_ID"))))

		(if (is_nil file_item) (
			(var file_item (func create_file (latest_release session content_type data)))
			(var latest_release.properties.file_item file_item.uuid)
		) else (
			(func update_file (file_item content_type data))
		))
	) else (
		(# Create a new file and file item)
		(var file_item (func create_file (latest_release session content_type data)))
		(var latest_release.properties.file_item file_item.uuid)
	))
))

(render_json (hash) 204)