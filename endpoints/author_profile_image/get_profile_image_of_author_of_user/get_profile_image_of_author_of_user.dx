(# Get the access token)
(var access_token (get_header Authorization))
(func render_validation_errors ((list
	(func validate_auth_header_presence (access_token))
)))

(# Get the session)
(var session (func get_session (access_token)))

(# Check if the user is an admin)
(var is_admin (env.ADMINS.contains session.user_id))

(if is_admin (
	(func render_validation_errors ((list (hash (error (get_error 1002)) (status 403)))))
))

(# Get the author of the user)
(var authors (func get_author_table_objects (session.user_id)))

(if (authors.length == 0) (
	(func render_validation_errors ((list (hash (error (get_error 3000)) (status 400)))))
))

(var author authors#0)

(# Define function for rendering no profile image error)
(def render_no_profile_image () (
	(func render_validation_errors ((list (hash (error (get_error 3505)) (status 404)))))
))

(# Get the profile image table object of the author)
(var profile_image_uuid author.properties.profile_image)

(if (is_nil profile_image_uuid) (
	(# Profile image does not exist)
	(func render_no_profile_image ())
))

(# Get the profile image table object)
(var profile_image (func get_table_object (profile_image_uuid env.AUTHOR_PROFILE_IMAGE_TABLE_ID)))

(if (is_nil profile_image) (
	(func render_no_profile_image ())
))

(# Get the profile image file)
(var profile_image_file (func get_table_object_file (profile_image_uuid)))

(# Return the content of the file)
(render_file profile_image_file profile_image.properties.type (author.properties.first_name + " " + author.properties.last_name + "." + profile_image.properties.ext) 200)