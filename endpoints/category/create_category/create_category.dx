(# Get the params)
(var fields_str (get_param "fields"))

(if (is_nil fields_str) (
	(var fields (list "uuid"))
) elseif (fields_str == "*") (
	(var fields (list "uuid" "key" "name"))
) else (
	(# Process the fields string)
	(var fields (func process_fields (fields_str)))
))

(var json (parse_json (get_body)))

(var key json.key)
(var name json.name)
(var language json.language)

(# Get the access token)
(var access_token (get_header "Authorization"))
(func render_validation_errors ((list
	(func validate_auth_header_presence (access_token))
)))

(# Make sure content type is json)
(func render_validation_errors ((list
	(func validate_content_type_json ((get_header "Content-Type")))
)))

(# Get the session)
(var session (func get_session (access_token)))

(# Check if the user is an admin)
(var admins (get_env "ADMINS"))
(var is_admin (admins.contains session.user_id))
(if (! is_admin) (
	(# Action not allowed)
	(func render_validation_errors ((list (hash (error (get_error 1002)) (status 403)))))
))

(# Validate missing fields)
(func render_validation_errors ((list
	(func validate_key_presence (key))
	(func validate_name_presence (name))
	(func validate_language_presence (language))
)))

(# Validate property types)
(func render_validation_errors ((list
	(func validate_key_type (key))
	(func validate_name_type (name))
	(func validate_language_type (language))
)))

(# Validate too short and too long fields)
(func render_validation_errors ((list
	(func validate_key_length (key))
	(func validate_name_length (name))
)))

(# Validate the key)
(func render_validation_errors ((list
	(func validate_key_validity (key))
)))

(# Validate the language)
(func render_validation_errors ((list
	(func validate_language_supported (language))
)))

(var key key.downcase)

(# Check if a category with the key already exists)
(var categories (TableObject.find_by_property admins#0 (get_env "CATEGORY_TABLE_ID") "key" key))

(if (categories.length > 0) (
	(# Key already taken)
	(func render_validation_errors ((list (hash (error (get_error 3200)) (status 422)))))
))

(# Create the category name)
(var category_name (func create_table_object (
	session.user_id
	(get_env "CATEGORY_NAME_TABLE_ID")
	(hash
		(name name)
		(language language)
	)
)))

(# Create the category)
(var category (func create_table_object (
	admins#0
	(get_env "CATEGORY_TABLE_ID")
	(hash
		(key key)
		(names category_name.uuid)
	)
)))

(# Render the result)
(var result (hash))

(if (fields.contains "uuid") (var result.uuid category.uuid))
(if (fields.contains "key") (var result.key category.properties.key))

(if (
	(fields.contains "name")
	or (fields.contains "name.value")
	or (fields.contains "name.language")
) (
	(var name_hash (hash))

	(if ((fields.contains "name") or (fields.contains "name.value")) (var name_hash.value name))
	(if ((fields.contains "name") or (fields.contains "name.language")) (var name_hash.language language))

	(var result.name name_hash)
))

(render_json result 201)