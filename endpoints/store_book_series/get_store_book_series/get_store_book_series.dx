(# Get the params)
(var uuid (get_param "uuid"))
(var fields_str (get_param "fields"))
(var language (get_param "language"))

(if (is_nil fields_str) (
	(var fields (list "uuid"))
) elseif (fields_str == "*") (
	(var fields (list "uuid" "name" "language"))
) else (
	(# Process the fields string)
	(var fields (func process_fields (fields_str)))
))

(if (is_nil language) (
	(var language "en")
))

(# Get the store book series)
(var series (func get_table_object (uuid (get_env "STORE_BOOK_SERIES_TABLE_ID"))))

(if (is_nil series) (
	(# Series does not exist)
	(func render_validation_errors ((list (hash (error (get_error 3514)) (status 404)))))
))

(# Get the names of the series)
(var names (func get_series_names_by_string (series.properties.names false)))

(if (names.length == 0) (
	(# StoreBookSeriesName does not exist)
	(func render_validation_errors ((list (hash (error (get_error 3515)) (status 412)))))
))

(var name nil)

(for n in names (
	(if (n.language == language) (
		(var name n)
		(break)
	))
))

(if (is_nil name) (
	(var name names#0)
))

(# Render the result)
(var result (hash))

(if (fields.contains "uuid") (var result.uuid series.uuid))
(if (fields.contains "name") (var result.name name.name))
(if (fields.contains "language") (var result.language name.language))

(render_json result 200)