(# Get the params)
(var fields_str (get_param "fields"))
(var languages (get_param "languages"))
(var limit (to_int (get_param "limit")))

(if (is_nil fields_str) (
	(var fields (list "authors.uuid"))
) elseif (fields_str == "*") (
	(var fields (list "authors.uuid" "authors.first_name" "authors.last_name" "authors.bio" "authors.website_url" "authors.facebook_username" "authors.instagram_username" "authors.twitter_username" "authors.profile_image_blurhash"))
) else (
	(# Process the fields string)
	(var fields (func process_fields (fields_str)))
))

(var language_list (func process_languages (languages)))

(if (limit <= 0) (
	(var limit 8)
))

(# Get the latest authors)
(var author_uuids (func get_latest_author_table_object_uuids ()))
(var author_uuids author_uuids.reverse)
(var authors (list))

(for author_uuid in author_uuids (
	(# Get the author table object)
	(var author (func get_table_object (author_uuid (get_env "AUTHOR_TABLE_ID"))))
	(if (is_nil author) (continue))

	(# Check if the author has uploaded a profile image)
	(if (!(is_nil author.properties.profile_image)) (
		(# Add the author to the list)
		(var author_hash (hash))

		(if (fields.contains "authors.uuid") (var author_hash.uuid author.uuid))
		(if (fields.contains "authors.first_name") (var author_hash.first_name author.properties.first_name))
		(if (fields.contains "authors.last_name") (var author_hash.last_name author.properties.last_name))
		(if (
			(fields.contains "authors.bio")
			or (fields.contains "authors.bio.value")
			or (fields.contains "authors.bio.language")
		) (
			(var author_hash.bio (
				(func find_author_bio_by_languages (
					author.properties.bios
					language_list
					((fields.contains "authors.bio") or (fields.contains "authors.bio.value"))
					((fields.contains "authors.bio") or (fields.contains "authors.bio.language"))
				))
			))
		))
		(if (fields.contains "authors.website_url") (var author_hash.website_url author.properties.website_url))
		(if (fields.contains "authors.facebook_username") (var author_hash.facebook_username author.properties.facebook_username))
		(if (fields.contains "authors.instagram_username") (var author_hash.instagram_username author.properties.instagram_username))
		(if (fields.contains "authors.twitter_username") (var author_hash.twitter_username author.properties.twitter_username))
		(if (fields.contains "authors.profile_image_blurhash") (var author_hash.profile_image_blurhash author.properties.profile_image_blurhash))

		(authors.push author_hash)

		(# Check if the limit is reached)
		(if (authors.length >= limit) (break))
	))
))

(# Render the result)
(render_json (hash
	(authors authors)
) 200)