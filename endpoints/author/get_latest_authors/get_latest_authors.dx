(# Get the params)
(var limit (to_int (get_param "limit")))

(if (limit <= 0) (
	(var limit 8)
))

(# Get the latest authors)
(var author_objects (func get_latest_author_table_objects ()))
(var author_objects author_objects.reverse)
(var authors (list))

(for author_object in author_objects (
	(# Check if the author has uploaded a profile image)
	(if (! (is_nil author_object.properties.profile_image)) (
		(# Convert the table object to hash)
		(var author (hash
			(uuid author_object.uuid)
			(first_name author_object.properties.first_name)
			(last_name author_object.properties.last_name)
			(website_url author_object.properties.website_url)
			(facebook_username author_object.properties.facebook_username)
			(instagram_username author_object.properties.instagram_username)
			(twitter_username author_object.properties.twitter_username)
			(profile_image true)
			(profile_image_blurhash author_object.properties.profile_image_blurhash)
		))

		(authors.push author)

		(# Check if the limit is reached)
		(if (authors.length >= limit) (break))
	))
))

(# Render the result)
(render_json (hash
	(authors authors)
) 200)