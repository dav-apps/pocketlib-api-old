(# Get the params)
(var fields_str (get_param "fields"))
(var languages (get_param "languages"))

(if (is_nil fields_str) (
	(var fields (list "authors.uuid"))
) elseif (fields_str == "*") (
	(var fields (list "authors.uuid" "authors.first_name" "authors.last_name" "authors.bio" "authors.website_url" "authors.facebook_username" "authors.instagram_username" "authors.twitter_username" "authors.profile_image" "authors.profile_image_blurhash"))
) else (
	(# Process the fields string)
	(var fields (func process_fields (fields_str)))
))

(var language_list (func process_languages (languages)))

(# Get the access token)
(var access_token (get_header "Authorization"))
(func render_validation_errors ((list
	(func validate_auth_header_presence (access_token))
)))

(# Get the session)
(var session (func get_session (access_token)))

(# Check if the user is an admin)
(var admins (get_env "ADMINS"))
(if (!(admins.contains session.user_id)) (
	(# User is not an admin)
	(func render_validation_errors ((list (hash (error (get_error 3014)) (status 400)))))
))

(# Get the authors of the user)
(var author_objects (func get_author_table_objects (session.user_id)))
(var authors (list))

(for author in author_objects (
	(var author_hash (hash))

	(if (fields.contains "authors.uuid") (var author_hash.uuid author.uuid))
	(if (fields.contains "authors.first_name") (var author_hash.first_name author.properties.first_name))
	(if (fields.contains "authors.last_name") (var author_hash.last_name author.properties.last_name))
	(if (
		(fields.contains "authors.bio")
		or (fields.contains "authors.bio.value")
		or (fields.contains "authors.bio.language")
	) (
		(var author_hash.bio (
			(func find_author_bio_by_languages (
				author.properties.bios
				language_list
				((fields.contains "authors.bio") or (fields.contains "authors.bio.value"))
				((fields.contains "authors.bio") or (fields.contains "authors.bio.language"))
			))
		))
	))
	(if (fields.contains "authors.website_url") (var author_hash.website_url author.properties.website_url))
	(if (fields.contains "authors.facebook_username") (var author_hash.facebook_username author.properties.facebook_username))
	(if (fields.contains "authors.instagram_username") (var author_hash.instagram_username author.properties.instagram_username))
	(if (fields.contains "authors.twitter_username") (var author_hash.twitter_username author.properties.twitter_username))
	(if (fields.contains "authors.profile_image") (var author_hash.profile_image (!(is_nil author.properties.profile_image))))
	(if (fields.contains "authors.profile_image_blurhash") (var author_hash.profile_image_blurhash author.properties.profile_image_blurhash))

	(authors.push author_hash)
))

(# Render the result)
(render_json (hash (authors authors)) 200)