(# Get the params)
(var uuid (get_param "uuid"))
(var fields_str (get_param "fields"))
(var languages (get_param "languages"))

(if (is_nil fields_str) (
	(var fields (list "uuid"))
) elseif (fields_str == "*") (
	(var fields (list "uuid" "first_name" "last_name" "bio" "website_url" "facebook_username" "instagram_username" "twitter_username" "profile_image"))
) else (
	(# Process the fields string)
	(var fields (func process_fields (fields_str)))
))

(var language_list (func process_languages (languages)))

(if (uuid == "mine") (
	(# Get the access token)
	(var access_token (get_header "Authorization"))
	(func render_validation_errors ((list
		(func validate_auth_header_presence (access_token))
	)))

	(# Get the session)
	(var session (func get_session (access_token)))

	(# Check if the user is an admin)
	(var admins (get_env "ADMINS"))
	(if (admins.contains session.user_id) (
		(# User is an admin)
		(func render_validation_errors ((list (hash (error (get_error 3013)) (status 400)))))
	))

	(# Get the author of the user)
	(var author_objects (func get_author_table_objects (session.user_id)))
	(if (author_objects.length == 0) (
		(# User is not an author)
		(func render_validation_errors ((list (hash (error (get_error 3000)) (status 400)))))
	))

	(var author author_objects#0)
) else (
	(# Get the author)
	(var author (func get_table_object (uuid (get_env "AUTHOR_TABLE_ID"))))

	(if (is_nil author) (
		(# Author does not exist)
		(func render_validation_errors ((list (hash (error (get_error 3503)) (status 404)))))
	))
))

(if (
	(fields.contains "profile_image")
	or (fields.contains "profile_image.url")
	or (fields.contains "profile_image.blurhash")
) (
	(# Get the profile image item)
	(var profile_image_item (func get_table_object (author.properties.profile_image_item (get_env "AUTHOR_PROFILE_IMAGE_ITEM_TABLE_ID"))))

	(if (!(is_nil profile_image_item)) (
		(# Get the profile image)
		(var profile_image (func get_table_object (profile_image_item.properties.profile_image (get_env "AUTHOR_PROFILE_IMAGE_TABLE_ID"))))
	))
))

(# Render the result)
(var result (hash))

(if (fields.contains "uuid") (var result.uuid author.uuid))
(if (fields.contains "first_name") (var result.first_name author.properties.first_name))
(if (fields.contains "last_name") (var result.last_name author.properties.last_name))
(if (
	(fields.contains "bio")
	or (fields.contains "bio.value")
	or (fields.contains "bio.language")
) (
	(var result.bio (
		(func find_author_bio_by_languages (
			author.properties.bios
			language_list
			((fields.contains "bio") or (fields.contains "bio.value"))
			((fields.contains "bio") or (fields.contains "bio.language"))
		))
	))
))
(if (fields.contains "website_url") (var result.website_url author.properties.website_url))
(if (fields.contains "facebook_username") (var result.facebook_username author.properties.facebook_username))
(if (fields.contains "instagram_username") (var result.instagram_username author.properties.instagram_username))
(if (fields.contains "twitter_username") (var result.twitter_username author.properties.twitter_username))
(if (
	(fields.contains "profile_image")
	or (fields.contains "profile_image.url")
	or (fields.contains "profile_image.blurhash")
) (
	(if ((is_nil profile_image_item) or (is_nil profile_image)) (
		(var result.profile_image nil)
	) else (
		(var profile_image_hash (hash))

		(if ((fields.contains "profile_image") or (fields.contains "profile_image.url")) (var profile_image_hash.url (TableObject.get_cdn_url profile_image_item.properties.profile_image (hash (etag profile_image.properties.etag)))))
		(if ((fields.contains "profile_image") or (fields.contains "profile_image.blurhash")) (var profile_image_hash.blurhash profile_image_item.properties.blurhash))

		(var result.profile_image profile_image_hash)
	))
))

(render_json result 200)