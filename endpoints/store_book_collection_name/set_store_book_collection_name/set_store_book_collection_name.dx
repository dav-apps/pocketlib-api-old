(# Get the params)
(var uuid (get_param "uuid"))
(var language (get_param "language"))

(var json (parse_json (get_body)))

(var name json.name)

(# Get the access token)
(var access_token (get_header "Authorization"))
(func render_validation_errors ((list
	(func validate_auth_header_presence (access_token))
)))

(# Make sure content type is json)
(func render_validation_errors ((list
	(func validate_content_type_json ((get_header "Content-Type")))
)))

(# Get the session)
(var session (func get_session (access_token)))

(# Get the collection)
(var collection (func get_table_object (uuid (get_env "STORE_BOOK_COLLECTION_TABLE_ID"))))

(if (is_nil collection) (
	(# Collection does not exist)
	(func render_validation_errors ((list (hash (error (get_error 3506)) (status 404)))))
))

(# Get the author)
(var author (func get_table_object (collection.properties.author (get_env "AUTHOR_TABLE_ID"))))

(if (is_nil author) (
	(# Author does not exist)
	(func render_validation_errors ((list (hash (error (get_error 3503)) (status 404)))))
))

(# Check if the author belongs to the user)
(if (author.user_id != session.user_id) (
	(func render_validation_errors ((list (hash (error (get_error 1002)) (status 403)))))
))

(# Validate missing fields)
(func render_validation_errors ((list
	(func validate_name_presence (name))
)))

(# Validate property types)
(func render_validation_errors ((list
	(func validate_name_type (name))
)))

(# Validate too short and too long fields)
(func render_validation_errors ((list
	(func validate_name_length (name))
)))

(# Validate the language)
(func render_validation_errors ((list
	(func validate_language_supported (language))
)))

(# Get the names of the collection)
(var names (func get_collection_names_by_string (collection.properties.names true)))

(# Check if the name in the given language already exists)
(var name_uuid nil)
(for collection_name in names (
	(if (collection_name.language == language) (
		(var name_uuid collection_name.uuid)
	))
))

(if (is_nil name_uuid) (
	(# Create a new name)
	(var name_obj (func create_table_object (session.user_id (get_env "STORE_BOOK_COLLECTION_NAME_TABLE_ID") (hash (name name) (language language)))))

	(# Update the collection with the new name uuid)
	(func add_name_to_collection (collection name_obj.uuid))
) else (
	(# Update the existing name)
	(func update_table_object (name_uuid (hash (name name))))
	(var name_obj (func get_table_object (name_uuid (get_env "STORE_BOOK_COLLECTION_NAME_TABLE_ID"))))
))

(# Render the result)
(render_json (hash
	(name name_obj.properties.name)
	(language name_obj.properties.language)
))